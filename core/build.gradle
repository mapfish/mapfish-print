import org.apache.tools.ant.taskdefs.condition.Os
import org.apache.tools.ant.filters.ReplaceTokens

apply plugin: 'war'
apply plugin: 'distribution'

defaultTasks 'build'

repositories {
    mavenLocal() {
        content {
            excludeModule("javax.media", "jai_core")
        }
    }
    mavenCentral {
        content {
            excludeModule("javax.media", "jai_core")
        }
    }
    maven { url "https://jaspersoft.jfrog.io/jaspersoft/third-party-ce-artifacts" }
    maven { url "https://repo.osgeo.org/repository/release/" }
}

// the task `run` of the gretty plugin conflicts with the task with the same
// name of the plugin `application`. that's why the following tasks are defined
// manually.
tasks.register('startScripts', CreateStartScripts) {
}
tasks.register('print', JavaExec) {
    mainClass = 'org.mapfish.print.cli.Main'
    classpath = sourceSets.main.runtimeClasspath
}

// define what should be included as distribution.
distributions {
    main {
        contents {
            from 'src/dist'

            into('lib') {
                from(jar)
                from(project.configurations.runtimeClasspath)
            }
            into('bin') {
                from(startScripts)
                fileMode = 0755
            }
        }
    }
}


def appDir = new File(project.buildDir, 'install')
installDist.doFirst {
    appDir.deleteDir()
}
tasks.withType(Javadoc).configureEach {
    options.encoding = 'utf-8'
    title = "Mapfish Print Core Module $version"
    if (JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}
tasks.register('testCLI', Exec) {
    dependsOn installDist
    workingDir new File(appDir, 'core/bin')
    def cmd
    if (Os.isFamily(Os.FAMILY_WINDOWS)) {
        cmd = ['cmd', '/c', 'print.bat']
    } else {
        cmd = ['./print']
    }

    File outputFile = new File(appDir, 'cliTestOutput.png')
    File configFile = file('src/test/resources/org/mapfish/print/cli/config.yaml')
    File v3ApiRequestFile = file('src/test/resources/org/mapfish/print/cli/v3Request.json')
    cmd.addAll([
        '-config', configFile.getAbsolutePath(),
        '-spec', v3ApiRequestFile.getAbsolutePath(),
        '-output', outputFile.getAbsolutePath()])
    commandLine cmd
    environment('JAVA_OPTS', '-Xmx500m')
    doFirst {
        println("Testing CLI application: $workingDir")
        outputFile.delete()
        assert !outputFile.exists()
    }
    doLast {
        assert outputFile.exists()
        assert outputFile.length() > 0
    }
}

test {
    maxHeapSize = "500M"
    reports {
        junitXml.getRequired().set(true)
        html.getRequired().set(true)
    }
}

build {
    dependsOn(tasks.distZip)
    dependsOn(tasks.testCLI)
    processTestResources.mustRunAfter(javadoc)
}

configurations {
    compile.transitive = true

    metrics {
        description = 'Libraries for measuring performance and load. See https://github.com/dropwizard/metrics'
    }
    implementation.extendsFrom(metrics)
    geotools {
        description = 'Geotools spatial libraries'
    }
    implementation.extendsFrom(geotools)
    jasper {
        description = 'Dependencies for the jasper reports generation'
    }
    implementation.extendsFrom(jasper)

    // Want to use the slf4j bridge instead
    all*.exclude group: 'commons-logging'
    // Obscure stack trace if we let this dependency: https://stackoverflow.com/a/15808940
    all*.exclude module: 'xercesImpl'
}



dependencies {
    implementation(
            "org.springframework:spring-context:5.3.29",
            "org.springframework:spring-web:5.3.29",
            "org.springframework:spring-webmvc:5.3.29",
            "org.springframework.security:spring-security-config:5.8.5",
            "org.springframework.security:spring-security-web:5.8.5",
            'com.thetransactioncompany:cors-filter:2.10',
            // Hibernate & Postgres
            'org.hibernate:hibernate-core:5.6.15.Final',
            'org.postgresql:postgresql:42.6.0',
            'com.vladmihalcea:hibernate-types-52:2.21.1',
            'com.mchange:c3p0:0.9.5.5',
            "org.springframework:spring-aspects:5.3.29",
            "org.springframework:spring-orm:5.3.29",
            "org.springframework:spring-jdbc:5.3.29",
            "org.springframework:spring-tx:5.3.29",
            "org.springframework:spring-test:5.3.29",
            "javax.media:jai-core:1.1.3"
    )
    metrics(
            "io.dropwizard.metrics:metrics-core:4.2.19",
            "io.dropwizard.metrics:metrics-servlet:4.2.19",
            "io.dropwizard.metrics:metrics-httpclient:4.2.19",
            "io.dropwizard.metrics:metrics-servlets:4.2.19",
            "io.dropwizard.metrics:metrics-jvm:4.2.19",
            "io.dropwizard.metrics:metrics-jmx:4.2.19",
            "io.dropwizard.metrics:metrics-logback:4.2.19",
    )
    geotools(
            "org.geotools:gt-epsg-hsql:29.2",
            "org.geotools:gt-render:29.2",
            "org.geotools:gt-geojson:29.2",
            "org.geotools:gt-geotiff:29.2",
            "org.geotools:gt-wms:29.2",
            "org.geotools.xsd:gt-xsd-gml3:29.2",
            "org.geotools:gt-svg:29.2",
            "org.geotools:gt-cql:29.2"
    )
    jasper(
            "net.sf.jasperreports:jasperreports:6.20.5",
            "org.codehaus.groovy:groovy-all:3.0.18",
            'ar.com.fdvs:DynamicJasper:5.3.8',
            "net.sf.jasperreports:jasperreports-fonts:6.20.5",
            "net.sf.jasperreports:jasperreports-functions:6.20.5",
            'joda-time:joda-time:2.12.5'
    )
    implementation(
            'com.google.guava:guava:32.1.2-jre',
            "org.slf4j:slf4j-api:2.0.7",
            "org.slf4j:jcl-over-slf4j:2.0.7",
            "org.slf4j:jul-to-slf4j:2.0.7",
            "ch.qos.logback:logback-classic:1.4.8",
            "ch.qos.logback:logback-access:1.4.8",
            'org.json:json:20230618',
            'org.yaml:snakeyaml:2.0',
            'com.github.spullara.cli-parser:cli-parser:1.1.6',
            'org.apache.httpcomponents:httpclient:4.5.14',
            'com.sun.mail:javax.mail:1.6.2',
            'com.amazonaws:aws-java-sdk-s3:1.12.520',
            'com.adobe.xmp:xmpcore:6.1.11',
            'io.sentry:sentry-logback:6.27.0',
            'net.logstash.logback:logstash-logback-encoder:7.4',
    )

    implementation(
            'org.apache.xmlgraphics:xmlgraphics-commons:2.8',
            "org.apache.xmlgraphics:batik-transcoder:1.16",
            "org.apache.xmlgraphics:batik-bridge:1.16",
            "org.apache.xmlgraphics:batik-codec:1.16",
            "org.apache.xmlgraphics:batik-svg-dom:1.16"
    )

    providedCompile('javax.servlet:javax.servlet-api:4.0.1')
    compileOnly "com.github.spotbugs:spotbugs-annotations:4.7.3"

    testImplementation group: 'de.saly', name: 'javamail-mock2-fullmock', version: '0.5-beta4'
}

static def gitRevision() {
    def gitRev = System.getenv('GIT_HEAD')
    return gitRev != null ? gitRev : "git rev-parse HEAD".execute().text.trim()
}

compileJava.options.compilerArgs = ['-Xlint:deprecation', '-Xlint:unchecked']
compileTestJava.options.compilerArgs = ['-Xlint:deprecation', '-Xlint:unchecked']

// Add version to resources, used for User-Agent string:
processResources {
    filter ReplaceTokens, tokens: [
        "application.version": version
    ]
}

def addManifestAttribute(manifest) {
    manifest.mainAttributes(
            'Mapfish-Print-Version': version,
            'Git-Revision': gitRevision(),
            'Build-Time': new Date().getDateTimeString(),
            'Build-By': System.getProperty('user.name', "unknown"),
            'Build-Java-Runtime-Name': System.getProperty('java.runtime.name', "unknown"),
            'Build-Java-Version': System.getProperty('java.version', "unknown"),
            'Build-OS-Arch': System.getProperty('os.arch', "unknown"),
            'Build-OS-Name': System.getProperty('os.name', "unknown")
    )
}

jar {
    enabled = true
    archiveBaseName = 'print-lib'
    addManifestAttribute(manifest)
}

war {
    archiveBaseName = 'print-servlet'
    addManifestAttribute(manifest)
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

tasks.register('show') {
}

show.doLast {
    war.classpath.files.each { println it }
}

tasks.register('libJavadocJar', Jar) {
    dependsOn javadoc
    archiveBaseName = 'print-lib'
    archiveClassifier = 'javadoc'
    from 'build/docs/javadoc'
}


tasks.register('libSourcesJar', Jar) {
    archiveBaseName = 'print-lib'
    from sourceSets.main.allSource
    archiveClassifier = 'sources'
}


gradle.taskGraph.whenReady { taskGraph ->
    if (taskGraph.hasTask(print)) {
        if (project.hasProperty('printArgs')) {
            print.args printArgs.toString().split(" ").toList()
        } else {
            throw new IllegalArgumentException('You must supply the -PprintArgs="..." arguments.\nFor Example:\n\n./gradlew print ' +
                    '-PprintArgs="-config examples/config.yaml -spec examples/spec.json -output ./output.pdf"\n\n')
        }
    }
}

startScripts {
    // Clear up the classpath because the launcher jar has it.
    applicationName = 'print'
    mainClass = 'org.mapfish.print.cli.Main'
    classpath = files(jar.archiveFile)
    outputDir = new File(project.buildDir, 'scripts')

    doLast {
        fileTree(startScripts.outputDir).each { file ->
            def encoding = 'UTF-8'
            // There was a bug in the start script where all jars were listed and that failed in windows.
            // now I just have the main jar and replace it with * so that all jars in that directory
            // are loaded on classpath.
            file.setText(file.getText(encoding).replace(jar.archiveFile.get().getAsFile().name, '*'), encoding)
        }
    }
}

tasks.register('explodedWar', Sync) {
    into "${buildDir}/webapp"
    with war
}
