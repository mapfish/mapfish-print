import org.apache.tools.ant.taskdefs.condition.Os
import org.apache.tools.ant.filters.ReplaceTokens

apply plugin: 'war'
apply plugin: 'distribution'

defaultTasks 'build'

repositories {
    mavenCentral {
        content {
            excludeModule('javax.media', 'jai_core')
        }
    }
    maven { url = 'https://jaspersoft.jfrog.io/jaspersoft/third-party-ce-artifacts' }
    maven { url = 'https://repo.osgeo.org/repository/release/' }
}

// the task `run` of the gretty plugin conflicts with the task with the same
// name of the plugin `application`. that's why the following tasks are defined
// manually.
tasks.register('startScripts', CreateStartScripts) {
}
tasks.register('print', JavaExec) {
    mainClass = 'org.mapfish.print.cli.Main'
    classpath = sourceSets.main.runtimeClasspath
}

// define what should be included as distribution.
distributions {
    main {
        contents {
            from 'src/dist'

            into('lib') {
                from(jar)
                from(project.configurations.runtimeClasspath)
            }
            into('bin') {
                from(startScripts)
                filePermissions { unix(0755) }
            }
        }
    }
}


def appDir = new File(getLayout().getBuildDirectory().getAsFile().get(), 'install')
installDist.doFirst {
    appDir.deleteDir()
}
tasks.withType(Javadoc).configureEach {
    options.encoding = 'utf-8'
    title = "Mapfish Print Core Module $version"
    if (JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}
tasks.register('testCLI', Exec) {
    dependsOn installDist
    workingDir new File(appDir, 'core/bin')
    def cmd
    if (Os.isFamily(Os.FAMILY_WINDOWS)) {
        cmd = ['cmd', '/c', 'print.bat']
    } else {
        cmd = ['./print']
    }

    File outputFile = new File(appDir, 'cliTestOutput.png')
    File configFile = file('src/test/resources/org/mapfish/print/cli/config.yaml')
    File v3ApiRequestFile = file('src/test/resources/org/mapfish/print/cli/v3Request.json')
    cmd.addAll([
        '-config', configFile.getAbsolutePath(),
        '-spec', v3ApiRequestFile.getAbsolutePath(),
        '-output', outputFile.getAbsolutePath()])
    commandLine cmd
    environment('JAVA_OPTS', '-Xmx500m')
    doFirst {
        println("Testing CLI application: $workingDir")
        outputFile.delete()
        assert !outputFile.exists()
    }
    doLast {
        assert outputFile.exists()
        assert outputFile.length() > 0
    }
}

test {
    reports {
        junitXml.getRequired().set(true)
        html.getRequired().set(true)
    }
    maxHeapSize = "2G"
    useJUnitPlatform()
}

build {
    dependsOn(tasks.distZip)
    dependsOn(tasks.testCLI)
    processTestResources.mustRunAfter(javadoc)
}

configurations {
    compile.transitive = true

    metrics {
        description = 'Libraries for measuring performance and load. See https://github.com/dropwizard/metrics'
    }
    implementation.extendsFrom(metrics)
    geotools {
        description = 'Geotools spatial libraries'
    }
    implementation.extendsFrom(geotools)
    jasper {
        description = 'Dependencies for the jasper reports generation'
    }
    implementation.extendsFrom(jasper)

    // Want to use the slf4j bridge instead
    all*.exclude group: 'commons-logging'
    // Obscure stack trace if we let this dependency: https://stackoverflow.com/a/15808940
    all*.exclude module: 'xercesImpl'
}



dependencies {
    implementation(
        'org.springframework:spring-context:7.0.1',
        'org.springframework:spring-core:7.0.1',
        'org.springframework:spring-web:7.0.1',
        'org.springframework:spring-webmvc:7.0.1',
        'org.springframework:spring-aspects:7.0.1',
        'org.springframework:spring-orm:7.0.1',
        'org.springframework:spring-jdbc:7.0.1',
        'org.springframework:spring-tx:7.0.1',
        'org.springframework:spring-test:7.0.1',
        'org.springframework.security:spring-security-config:7.0.1',
        'org.springframework.security:spring-security-web:7.0.1',
        'com.thetransactioncompany:cors-filter:3.1',
        'org.hibernate:hibernate-core:6.6.38.Final',
        'org.postgresql:postgresql:42.7.8',
        'io.hypersistence:hypersistence-utils-hibernate-63:3.13.2',
        'com.mchange:c3p0:0.11.2',
        'javax.media:jai-core:1.1.3',
        'jakarta.annotation:jakarta.annotation-api:3.0.0',
        'jakarta.persistence:jakarta.persistence-api:3.0.0',
        'jakarta.servlet:jakarta.servlet-api:6.1.0'
    )
    metrics(
        'io.dropwizard.metrics:metrics-core:4.2.37',
        'io.dropwizard.metrics:metrics-jakarta-servlet:4.2.37',
        'io.dropwizard.metrics:metrics-jakarta-servlets:4.2.37',
        'io.dropwizard.metrics:metrics-httpclient:4.2.37',
        'io.dropwizard.metrics:metrics-jvm:4.2.37',
        'io.dropwizard.metrics:metrics-jmx:4.2.37',
        'io.dropwizard.metrics:metrics-logback:4.2.37',
    )
    geotools(
        'org.geotools:gt-epsg-hsql:34.1',
        'org.geotools:gt-render:34.1',
        'org.geotools:gt-geojson:34.1',
        'org.geotools:gt-geotiff:34.1',
        'org.geotools:gt-wms:34.1',
        'org.geotools.xsd:gt-xsd-gml3:34.1',
        'org.geotools:gt-svg:34.1',
        'org.geotools:gt-cql:34.1',
    )
    implementation "org.apache.httpcomponents.client5:httpclient5:5.4.4"
    jasper(
        'ar.com.fdvs:DynamicJasper:5.3.9',
        'com.itextpdf:itextpdf:5.5.13.4',
        'net.sf.jasperreports:jasperreports:7.0.3',
        'net.sf.jasperreports:jasperreports-excel-poi:7.0.3',
        'net.sf.jasperreports:jasperreports-fonts:7.0.3',
        'net.sf.jasperreports:jasperreports-functions:7.0.3',
        'net.sf.jasperreports:jasperreports-json:7.0.3',
        'net.sf.jasperreports:jasperreports-jdt:7.0.3',
        'net.sf.jasperreports:jasperreports-pdf:7.0.3',
    )
    implementation(
        'org.slf4j:slf4j-api:2.0.17',
        'org.slf4j:jcl-over-slf4j:2.0.17',
        'org.slf4j:jul-to-slf4j:2.0.17',
            'ch.qos.logback:logback-classic:1.5.23',
            'ch.qos.logback:logback-access:1.5.23',
        'org.json:json:20250517',
        'org.yaml:snakeyaml:2.5',
        'com.github.spullara.cli-parser:cli-parser:1.1.6',
        'com.sun.mail:javax.mail:1.6.2',
            'com.amazonaws:aws-java-sdk-s3:1.12.797',
            'io.sentry:sentry-logback:8.29.0',
        // For JSON logs
        'net.logstash.logback:logstash-logback-encoder:9.0',
        // For PDF/A
        'com.adobe.xmp:xmpcore:6.1.11',
        // For JasperReports
        'joda-time:joda-time:2.14.0',
        'org.jfree:jcommon:1.0.24',
            'org.apache.groovy:groovy-all:5.0.3',
        'xalan:serializer:2.7.3',
        // For WebP images
            'com.twelvemonkeys.imageio:imageio-webp:3.13.0',
    )

    implementation(
        'org.apache.xmlgraphics:batik-codec:1.19',
        'org.apache.xmlgraphics:batik-svg-dom:1.19'
    )

    //providedCompile('jakarta.servlet:jakarta.servlet-api:6.1.0')
    compileOnly 'com.github.spotbugs:spotbugs-annotations:4.9.8'

    testImplementation (
        'de.saly:javamail-mock2-fullmock:0.5-beta4',
        'com.h2database:h2:2.4.240',
        'org.junit.jupiter:junit-jupiter:6.0.1',
        'org.junit.jupiter:junit-jupiter-api:6.0.1',
    )
}

static def gitRevision() {
    def gitRev = System.getenv('GIT_HEAD')
    return gitRev != null ? gitRev : 'git rev-parse HEAD'.execute().text.trim()
}

compileTestJava {
    options.deprecation = true
    options.compilerArgs = ['-Xlint:unchecked']
}

// Add version to resources, used for User-Agent string:
processResources {
    filter ReplaceTokens, tokens: [
        'application.version': version
    ]
}

def addManifestAttribute(manifest) {
    manifest.mainAttributes(
        'Mapfish-Print-Version': version,
        'Git-Revision': gitRevision(),
        'Build-Time': new Date().getDateTimeString(),
        'Build-By': System.getProperty('user.name', 'unknown'),
        'Build-Java-Runtime-Name': System.getProperty('java.runtime.name', 'unknown'),
        'Build-Java-Version': System.getProperty('java.version', 'unknown'),
        'Build-OS-Arch': System.getProperty('os.arch', 'unknown'),
        'Build-OS-Name': System.getProperty('os.name', 'unknown')
    )
}

jar {
    enabled = true
    archiveBaseName = 'print-lib'
    addManifestAttribute(manifest)
}

war {
    archiveBaseName = 'print-servlet'
    addManifestAttribute(manifest)
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    webAppDirectory = file('src/main/webapp')
}

tasks.register('show') {
}

show.doLast {
    war.classpath.files.each { println it }
}

tasks.register('libJavadocJar', Jar) {
    dependsOn javadoc
    archiveBaseName = 'print-lib'
    archiveClassifier = 'javadoc'
    from 'build/docs/javadoc'
}


tasks.register('libSourcesJar', Jar) {
    archiveBaseName = 'print-lib'
    from sourceSets.main.allSource
    archiveClassifier = 'sources'
}


gradle.taskGraph.whenReady { taskGraph ->
    if (taskGraph.hasTask(print)) {
        if (project.hasProperty('printArgs')) {
            print.args printArgs.toString().split(" ").toList()
        } else {
            throw new IllegalArgumentException('You must supply the -PprintArgs="..." arguments.\nFor Example:\n\n./gradlew print ' +
                '-PprintArgs="-config examples/config.yaml -spec examples/spec.json -output ./output.pdf"\n\n')
        }
    }
}

startScripts {
    // Clear up the classpath because the launcher jar has it.
    applicationName = 'print'
    mainClass = 'org.mapfish.print.cli.Main'
    classpath = files(jar.archiveFile)
    outputDir = new File(project.buildDir, 'scripts')

    doLast {
        fileTree(startScripts.outputDir).each { file ->
            def encoding = 'UTF-8'
            // There was a bug in the start script where all jars were listed and that failed in windows.
            // now I just have the main jar and replace it with * so that all jars in that directory
            // are loaded on classpath.
            file.setText(file.getText(encoding).replace(jar.archiveFile.get().getAsFile().name, '*'), encoding)
        }
    }
}

tasks.register('explodedWar', Sync) {
    into "${buildDir}/webapp"
    with war
}

tasks.withType(Test).configureEach { t ->
    afterSuite { desc, result ->
        if (desc.parent != null) return // only print once, for the outermost suite
        println ""
        println "Core test ${t.path} summary: ${result.resultType} " +
            "(${result.testCount} tests, " +
            "${result.successfulTestCount} passed, " +
            "${result.failedTestCount} failed, " +
            "${result.skippedTestCount} skipped)"
    }
}
