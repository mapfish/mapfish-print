apply plugin: 'groovy'
apply plugin: 'eclipse'
apply plugin: 'idea'

defaultTasks 'build'

repositories {
    mavenCentral {
        content {
            excludeModule("javax.media", "jai_core")
        }
    }
    maven { url "https://jaspersoft.jfrog.io/jaspersoft/third-party-ce-artifacts" }
    maven { url "https://repo.osgeo.org/repository/release/" }
}

dependencies {
    implementation project(':core')
    implementation ("org.codehaus.groovy:groovy-all:3.0.19",
            "org.springframework:spring-test:5.3.30",
            'org.ccil.cowan.tagsoup:tagsoup:1.2.1',
            'com.samskivert:jmustache:1.15',
            'com.google.guava:guava:32.1.3-jre',
            'commons-io:commons-io:2.15.0',
            "org.springframework:spring-beans:5.3.30",
            "org.springframework:spring-web:5.3.30",
            "org.springframework:spring-context:5.3.30",
            'javax.servlet:javax.servlet-api:4.0.1'
    )
}

def site = new File(buildDir, "site").path

tasks.register('copyJavadocs', Copy) {
    dependsOn ':core:javadoc'
    from project(':core').tasks.javadoc.destinationDir
    into new File(site, 'javadoc')
}

tasks.register('copySite', Copy) {
    dependsOn ':docs:processResources'
    from(sourceSets.main.output.resourcesDir) {
        exclude 'pages.json', 'templates/*', 'templates'
    }
    from(project(':core').webAppDir) {
        include 'favicon.ico', 'mapfish_transparent.png'
    }

    into site
}

tasks.register('buildDocs', JavaExec) {
    dependsOn compileGroovy
    dependsOn copyJavadocs
    dependsOn copySite

    mainClass = 'GenerateDocs'
    classpath = sourceSets.main.runtimeClasspath

    def javadocs = project(':core').tasks.javadoc.destinationDir
    def basicAppContext = 'classpath*:mapfish-spring-application-context.xml'
    def appContext = project(':core').projectDir.toURI().toString() + '/jettyRunExtraFiles/*.xml'
    def appContext2 = project(':core').webAppDir.toURI().toString() + '/WEB-INF/mapfish-print-servlet.xml'
    def overrideAppContext = 'classpath*:mapfish-spring-application-context-override.xml'
    args site, javadocs, basicAppContext, appContext, appContext2, overrideAppContext
    systemProperty 'path_to_examples', "${project(':examples').projectDir}/src/test/resources/examples"
}

build {
    dependsOn buildDocs
}
