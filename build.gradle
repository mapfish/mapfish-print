buildscript {
    repositories {
        mavenCentral()
        maven { url "https://plugins.gradle.org/m2/" }
    }

    dependencies {
        classpath "gradle.plugin.com.github.spotbugs.snom:spotbugs-gradle-plugin:4.7.5"
    }
}

defaultTasks 'build'

wrapper {
    gradleVersion = '7.6.2'
    distributionType = Wrapper.DistributionType.ALL
}

allprojects {
    group = group
    version = version
}

configurations.configureEach {
    resolutionStrategy {
        force 'xml-apis:xml-apis:2.0.2'
    }
}

configure(subprojects.findAll { ['core', 'examples'].contains(it.name) }) {
    apply plugin: 'java'
    apply plugin: 'com.github.spotbugs'
    apply plugin: 'eclipse'
    apply plugin: 'idea'
    apply plugin: 'project-report'
    apply plugin: 'checkstyle'

    jar.dependsOn(checkstyleMain)

    checkstyle {
        toolVersion = "10.12.1" // com.puppycrawl.tools:checkstyle
        configFile = file("$rootProject.rootDir/checkstyle_checks.xml")
        configProperties = ['basedir': project.rootDir.path]
        reportsDir = file("$project.buildDir/reports/checkstyleReports")
    }

    spotbugs {
        toolVersion = "4.4.2" // com.github.spotbugs:spotbugs-annotations
        reportsDir = file("$project.buildDir/reports/spotbugsReports")
        effort = "max"
        reportLevel = "high"
    }

    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11

    compileJava.options.compilerArgs = ['-Xlint:deprecation', '-Xlint:unchecked']

    dependencies {
        testImplementation(
                "junit:junit:4.13.2",
                'org.mockito:mockito-core:5.4.0',
        )
    }

    test {
        jvmArgs '-XX:+CMSClassUnloadingEnabled'
        minHeapSize = "128m"
        maxHeapSize = "512m"
        testLogging {
            exceptionFormat = 'full'
        }
    }

    tasks.withType(JavaCompile).configureEach {
        options.encoding = 'utf-8'
    }
    tasks.withType(Javadoc).tap {
        configureEach {
            options.encoding = 'utf-8'
        }
    }
}

tasks.register('docs') {
    enabled = false
}

