buildscript {
    repositories {
        mavenLocal()
        jcenter()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }

    dependencies {
        classpath "gradle.plugin.com.github.spotbugs:spotbugs-gradle-plugin:2.0.0"
        classpath "se.bjurr.violations:violations-gradle-plugin:1.47"
        classpath 'org.owasp:dependency-check-gradle:6.0.2'
    }
}

defaultTasks 'build'

wrapper {
    gradleVersion = '5.5.1'
    distributionType = Wrapper.DistributionType.ALL
}

allprojects {
    group = 'org.mapfish.print'
    version = '3.25.0'
    ext.junitVersion = "4.13"
    ext.groovyVersion = "3.0.6"
    ext.spotbugVersion = '3.1.12'
    ext.jasperreportVersion = "6.14.0"

    apply plugin: 'org.owasp.dependencycheck'
    dependencyCheck {
        outputDirectory = "security-report"
    }
}

configurations.all {
    resolutionStrategy {
        force 'xml-apis:xml-apis:2.0.2'
    }
}

apply plugin: 'org.owasp.dependencycheck'

configure(subprojects.findAll { ['core', 'examples', 'docs'].contains(it.name) }) {
    repositories {
        mavenLocal()
        maven { url "http://download.osgeo.org/webdav/geotools" }
        mavenCentral()
        jcenter()
        maven { url "https://dl.bintray.com/readytalk/maven" }
        maven { url "https://jaspersoft.jfrog.io/jaspersoft/third-party-ce-artifacts" }
        maven { url "https://repo.osgeo.org/repository/release/" }
        maven { url "https://download.osgeo.org/webdav/geotools/" }
    }
}
configure(subprojects.findAll { ['core', 'examples'].contains(it.name) }) {
    apply plugin: 'java'
    apply plugin: 'com.github.spotbugs'
    apply plugin: 'eclipse'
    apply plugin: 'idea'
    apply plugin: 'project-report'
    apply plugin: 'checkstyle'
    apply plugin: 'se.bjurr.violations.violations-gradle-plugin'

    jar.dependsOn(checkstyleMain)

    checkstyle {
        toolVersion = "7.8.2"
        configFile = file("$rootProject.rootDir/checkstyle_checks.xml")
        configProperties = ['basedir': project.rootDir.path]
        ignoreFailures = true
        reportsDir = file("$project.buildDir/reports/checkstyleReports")
    }
    checkstyleTest.enabled = false

    spotbugs {
        toolVersion = "${project.spotbugVersion}"
        ignoreFailures = true
        reportsDir = file("$project.buildDir/reports/spotbugsReports")
        effort = "max"
        reportLevel = "high"
    }
    spotbugsTest.enabled = false

    task violations(type: se.bjurr.violations.gradle.plugin.ViolationsTask) {
        minSeverity = 'INFO'
        detailLevel = 'VERBOSE' // PER_FILE_COMPACT, COMPACT or VERBOSE
        maxViolations = 0
        printViolations = true
        violations = [
                ["FINDBUGS", ".", ".*/reports/spotbugsReports/.*\\.xml", "Spotbugs"],
                ["CHECKSTYLE", ".", ".*/reports/checkstyleReports/.*\\.xml", "Checkstyle"]
        ]
    }
    check.finalizedBy violations

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    compileJava.options.compilerArgs = ['-Xlint:deprecation', '-Xlint:unchecked']

    dependencies {
        testCompile(
                "junit:junit:${project.junitVersion}",
                'org.mockito:mockito-core:3.5.13',
        )
    }

    test {
        jvmArgs '-XX:+UseConcMarkSweepGC', '-XX:+CMSClassUnloadingEnabled'
        minHeapSize = "128m"
        maxHeapSize = "512m"
        testLogging {
            exceptionFormat = 'full'
        }
    }

    tasks.withType(JavaCompile) {
        options.encoding = 'utf-8'
    }
    tasks.withType(Javadoc) {
        options.encoding = 'utf-8'
    }
}
