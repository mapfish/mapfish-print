import se.bjurr.violations.gradle.plugin.ViolationsTask

buildscript {
    repositories {
        mavenCentral()
        maven { url "https://plugins.gradle.org/m2/" }
    }

    dependencies {
        classpath "gradle.plugin.com.github.spotbugs.snom:spotbugs-gradle-plugin:4.7.5"
        classpath "se.bjurr.violations:violations-gradle-plugin:1.52.6"
    }
}

defaultTasks 'build'

wrapper {
    gradleVersion = '7.6.2'
    distributionType = Wrapper.DistributionType.ALL
}

allprojects {
    group = group
    version = version
}

configurations.configureEach {
    resolutionStrategy {
        force 'xml-apis:xml-apis:2.0.2'
    }
}

configure(subprojects.findAll { ['core', 'examples'].contains(it.name) }) {
    apply plugin: 'java'
    apply plugin: 'com.github.spotbugs'
    apply plugin: 'eclipse'
    apply plugin: 'idea'
    apply plugin: 'project-report'
    apply plugin: 'checkstyle'
    apply plugin: 'se.bjurr.violations.violations-gradle-plugin'

    jar.dependsOn(checkstyleMain)

    checkstyle {
        toolVersion = "6.19" // com.puppycrawl.tools:checkstyle
        configFile = file("$rootProject.rootDir/checkstyle_checks.xml")
        configProperties = ['basedir': project.rootDir.path]
        ignoreFailures = true
        reportsDir = file("$project.buildDir/reports/checkstyleReports")
    }
    checkstyleTest.enabled = false

    spotbugs {
        toolVersion = "4.4.2" // com.github.spotbugs:spotbugs-annotations
        ignoreFailures = true
        reportsDir = file("$project.buildDir/reports/spotbugsReports")
        effort = "max"
        reportLevel = "high"
    }
    spotbugsTest.enabled = true

    tasks.register('violations', ViolationsTask) {
        minSeverity = 'INFO'
        detailLevel = 'VERBOSE' // PER_FILE_COMPACT, COMPACT or VERBOSE
        maxViolations = 1064
        printViolations = true
        violations = [
            ["FINDBUGS", ".", ".*/reports/spotbugsReports/.*\\.xml", "Spotbugs"],
            ["CHECKSTYLE", ".", ".*/reports/checkstyleReports/.*\\.xml", "Checkstyle"]
        ]
    }
    check.finalizedBy violations

    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11

    compileJava.options.compilerArgs = ['-Xlint:deprecation', '-Xlint:unchecked']

    dependencies {
        testImplementation(
                "junit:junit:4.13.2",
                'org.mockito:mockito-core:5.4.0',
        )
    }

    test {
        jvmArgs '-XX:+CMSClassUnloadingEnabled'
        minHeapSize = "128m"
        maxHeapSize = "512m"
        testLogging {
            exceptionFormat = 'full'
        }
    }

    tasks.withType(JavaCompile).configureEach {
        options.encoding = 'utf-8'
    }
    tasks.withType(Javadoc).tap {
        configureEach {
            options.encoding = 'utf-8'
        }
    }
}

tasks.register('docs') {
    enabled = false
}

