defaultTasks 'build'

task wrapper(type: Wrapper) {
    gradleVersion = '2.7'
    scriptFile = "generated-gradlew"
}

allprojects {
    group = 'org.mapfish.print'
    version = '3.17.0'
    ext.junitVersion = "4.12"
    ext.springVersion = "5.1.4.RELEASE"
    ext.groovyVersion = "2.5.5"
    ext."signing.keyId" = signing_keyId
    ext."signing.secretKeyRingFile" = System.properties.get('user.home') + '/.gnupg/secring.gpg'
    ext."signing.password" = signing_password
}

buildscript {
  repositories {
    jcenter()
  }

  dependencies {
      classpath 'org.akhikhl.gretty:gretty:2.0.0'
      classpath 'io.codearte.gradle.nexus:gradle-nexus-staging-plugin:0.12.0'
  }
}

configurations.all {
    resolutionStrategy {
        force 'xml-apis:xml-apis:2.0.2'
    }
}

apply plugin: 'org.akhikhl.gretty'

import org.akhikhl.gretty.AppStartTask
task appRun(type: AppStartTask) {
}

configure(subprojects.findAll {['core', 'examples', 'docs'].contains(it.name)}) {
    repositories {
        mavenLocal()
        mavenCentral()
        maven {url "http://download.osgeo.org/webdav/geotools"}
        maven {url "https://dl.bintray.com/readytalk/maven"}
        maven {url "http://jaspersoft.artifactoryonline.com/jaspersoft/third-party-ce-artifacts"}
        maven {url "http://maven.geo-solutions.it"}
        maven {url "https://jaspersoft.artifactoryonline.com/jaspersoft/jr-ce-releases"}
        maven {url "https://repository.jboss.org/nexus/content/repositories/thirdparty-releases/"}
    }
}
configure(subprojects.findAll {['core', 'examples'].contains(it.name)}) {
    apply plugin: 'java'
    apply plugin: 'findbugs'
    apply plugin: 'eclipse'
    apply plugin: 'idea'
    apply plugin: 'project-report'
    apply plugin: 'checkstyle'

    jar.dependsOn(checkstyleMain, findbugsMain)

    checkstyle {
        toolVersion = "7.8.2"
        configFile = file("$rootProject.rootDir/checkstyle_checks.xml")
        configProperties = ['basedir': project.rootDir.path]
        ignoreFailures = false
        reportsDir = file("$project.buildDir/reports/checkstyleReports")
    }
    checkstyleTest.enabled = false

    String fullVersion = System.getProperty("java.specification.version");
    String findbugsToolVersion = "3.0.1"
    if (fullVersion.startsWith("1.6")) {
        findbugsToolVersion = "2.0.3"
    }

    findbugs {
        toolVersion = findbugsToolVersion
        ignoreFailures = false
        reportsDir = file("$project.buildDir/reports/findbugsReports")
        effort = "max"
        reportLevel = "high"
        excludeFilter = file("$rootProject.projectDir/findbugs-exclude.xml")
    }
    tasks.withType(FindBugs) {
        reports {
            xml.enabled = false
            html.enabled = true
        }
    }
    findbugsTest.enabled = false

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    compileJava.options.compilerArgs = ['-Xlint:deprecation', '-Xlint:unchecked']

    dependencies {
        testCompile (
                "junit:junit:${project.junitVersion}",
                'org.mockito:mockito-core:2.24.0',
                "org.springframework:spring-test:${springVersion}"
        )

    }

    test {
        jvmArgs '-XX:+UseConcMarkSweepGC', '-XX:+CMSClassUnloadingEnabled'
        minHeapSize = "128m"
        maxHeapSize = "512m"
        testLogging {
            exceptionFormat = 'full'
        }
    }

    sourceSets {
        test {
            output.resourcesDir = output.classesDir
        }
    }

    tasks.withType(JavaCompile) {
        options.encoding = 'utf-8'
    }
    tasks.withType(Javadoc) {
        options.encoding = 'utf-8'
    }

}

if (Boolean.parseBoolean(enablePublishing)) {
    apply plugin: 'io.codearte.nexus-staging'

    nexusStaging {
        packageGroup = 'org.mapfish'
        stagingProfileId = '201d075c19938f'
        serverUrl = "https://$host/service/local/"
        username = sonatypeUsername
        password = sonatypePassword
        numberOfRetries = 200
    }
}
